//===== rAthena Script ======================================= 
//= Einbroch Airport Staff
//===== By: ================================================== 
//= L0ne_W0lf, Muad_Dib
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Einbroch Airport Staff
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

airport,143,43,5	script	Airport Staff#airport1a::airport1	90,{
	mes "[Funcionário do Aeroporto]";
	mes "Bem vindo ao";
	mes "Aeroporto de Einbroch,";
	mes "onde oferecemos vôos sem";
	mes "escalas para as cidades de";
	mes "Juno, Lighthalzen e Hugel.";
	next;
	if (select("Embarcar:Cancelar") == 1) {
		mes "[Funcionário do Aeroporto]";
		mes "A taxa de embarque do Dirigível";
		mes "é 1,200 zeny, mas se você tiver";
		mes "um Bilhete Gratuito para Dirigível,";
		mes "a taxa será dispensada. Você";
		mes "embarcará no Dirigível?";
		next;
		if (select("Sim:Não") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Free_Flying_Ship_Ticket
				warp "airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airport",148,51;
				end;
			}
			mes "[Funcionário do Aeroporto]";
			mes "Me desculpe, mas você não";
			mes "tem um Ticket Gratuito para";
			mes "Dirigível e você não tem";
			mes "zeny suficiente para embarcar";
			mes "no Dirigível. Lembre-se, a";
			mes "taxa de embarque é 1,200 zeny.";
			close;
		}
	}
	mes "[Funcionário do Aeroporto]";
	mes "Obrigado e";
	mes "tenha um bom dia.";
	close;
}

airport,158,43,5	duplicate(airport1)	Airport Staff#airport1b	90
airport,126,43,5	duplicate(airport1)	Airport Staff#airport1c	90

airport,143,49,3	script	Arrival Staff#airport2a::airport2	90,{
	mes "[Equipe de chegada]";
	mes "Bem vindo ao Aeroporto de Einbroch.";
	mes "Se você está chegando do seu";
	mes "vôo, deixe-me guiá-lo para o";
	mes "terminal principal. Caso contrário, embarque no Dirigível para partir para";
	mes "Juno, Lighthalzen ou Hugel.";
	next;
	if (select("Sair para o terminal principal.:Cancelar.") == 1) {
		mes "[Equipe de chegada]";
		mes "Uma vez que você estiver no terminal principal, você precisará pagar a taxa novamente";
		mes "para embarcar em um Dirigível. Você só";
		mes "deve sair se a cidade de Einbroch";
		mes "é o destino planejado. Devemos";
		mes "prosseguir para o terminal principal?";
		next;
		if (select("Sim:Não") == 1) {
			warp "airport",142,40;
			end;
		}
	}
	mes "[Equipe de chegada]";
	mes "Tudo bem, obrigado";
	mes "pelo seu patrocínio";
	mes "e espero que você tenha";
	mes "um vôo agradável~";
	close;
}

airport,126,51,3	duplicate(airport2)	Arrival Staff#airport2b	90
airport,158,50,3	duplicate(airport2)	Arrival Staff#airport2c	90

einbroch,94,267,3	script	Airship Staff#ein01	91,{
	mes "[Funcionário do Dirigível]";
	mes "Bem vindo ao";
	mes "Aeroporto de Einbroch.";
	mes "Use esta porta para";
	mes "embarcar no Dirigível que pára";
	mes "em Juno, Lighthalzen e";
	mes "Hugel na Republica de Schwaltzvalt.";
	next;
	mes "[Funcionário do Dirigível]";
	mes "Caso contrário, se Einbroch for";
	mes "seu destino planejado,";
	mes "basta descer as escadas";
	mes "e pedir ao pessoal da chegada para conduzí-lo";
	mes "para o terminal principal. Obrigado";
	mes "e aproveite suas viagens.";
	close;
}
